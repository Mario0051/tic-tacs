plugins {
  id 'fabric-loom' version '0.9-SNAPSHOT'
  id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
  accessWidenerPath = file("src/main/resources/tic_tacs.accesswidener")
}

configurations {
    modIncludeImplementation

    include.extendsFrom modIncludeImplementation
    modImplementation.extendsFrom modIncludeImplementation
}

repositories {
  maven { url = "https://jitpack.io" }
  maven { url = 'https://maven.gegy1000.net/' }
}

dependencies {
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

  // Fabric API
  modIncludeImplementation(fabricApi.module("fabric-api-base", project.fabric_version))
  modIncludeImplementation(fabricApi.module("fabric-networking-v0", project.fabric_version))

  implementation 'com.google.code.findbugs:jsr305:3.0.2'

  implementation 'net.gegy1000:just-now:0.1.0-SNAPSHOT'
  include 'net.gegy1000:just-now:0.1.0-SNAPSHOT'

  modRuntime ("com.github.SuperCoder7979:databreaker:0.2.5") {
    transitive = false
  }
}

processResources {
  inputs.property "version", project.version

  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

tasks.withType(JavaCompile) {
  options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = "sources"
  from sourceSets.main.allSource
}
